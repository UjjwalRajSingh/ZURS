 managed implementation in class zbp_urs_travel unique;
//Guideline and best practice for definition to be followed
strict ( 2 );
//add draft feature at BO level
with draft;

//Define the behavior for Root entity
define behavior for ZURS_TRAVEL alias Travel
//Make the behavior pool/implemetation for Travel
implementation in class zbp_urs_travel unique
//Telling the framework to insert data in this db table for travel request
persistent table /dmo/travel_m
//How the RAP should handle the locks - if a travel request is locked
//The other users cannot edit the childs - like its bookings and suppl
lock master
//mandatory to use total etag
total etag LastChangedAt
//controls the permissions and security settings for our RAP BO
authorization master ( instance )
//specify the draft table - will created by the rap framework
draft table zurs_d_travel
//controlling the concurrency
etag master LastChangedAt
early numbering
with additional save
{
  //Here are the operations which RAP with generate automatically (manage)
  create ( precheck );
  update ( precheck );
  delete;
  //A booking can only be created with travel request
  //dynamic feature control
  //Add DRAFT feature also for Booking
  association _Booking { create (features:instance); with draft; }

  //its a piece of code which is intented to be only
  //consumed within our RAP BO
  internal action reCalcTotalPrice;
  //Define determination to execute the code when
  //booking fee or curr code changes so we calc total price
  determination calculateTotalPrice on modify
            { create; field BookingFee, CurrencyCode; }

  //Checking custom business object rules
  validation validateHeaderData on save { create; field CustomerId, BeginDate, EndDate; }

  //Static feature control
  field (readonly) TravelId, TotalPrice, CreatedBy, CreatedAt, LastChangedBy, LastChangedAt;
  field (mandatory) AgencyId, CustomerId, BeginDate, EndDate, OverallStatus, BookingFee, CurrencyCode;

  //Data actions using factory action to create new instances of BO
  factory action copyTravel[1];

  //non factory actions which will change the state of the BO instance
  action (features: instance) acceptTravel result[1] $self;
  action (features: instance) rejectTravel result[1] $self;

  //create a new determine action
  determine action validationCustomer { validation validateHeaderData; }
  //Adding side-effect which inform RAP to reaload the total price if the booking
  //fee has been changed on the Frontend
  side effects {
    field BookingFee affects field TotalPrice;
    determine action validationCustomer executed on field CustomerId affects messages;
  }

  //adding the draft actions
  draft determine action Prepare;
  draft action Edit;
  draft action Resume;
  draft action Activate;
  draft action Discard;

  mapping for /dmo/travel_m{
    TravelId = travel_id;
    AgencyId = agency_id;
    CustomerId = customer_id;
    BeginDate = begin_date;
    EndDate = end_date;
    TotalPrice = total_price;
    BookingFee = booking_fee;
    CurrencyCode = currency_code;
    Description = description;
    OverallStatus = overall_status;
    CreatedBy = created_by;
    LastChangedBy = last_changed_by;
    CreatedAt = created_at;
    LastChangedAt = last_changed_at;
  }
}

//Define the behavior for child entity
define behavior for ZURS_BOOKING alias Booking
//Make the behavior pool/implemetation for Booking
implementation in class zbp_urs_booking unique
//Telling the framework to insert data in this db table for booking
persistent table /dmo/booking_m
//draft table for the booking
draft table zurs_d_book
lock dependent by _Travel
authorization dependent by _Travel
etag master LastChangedAt
early numbering
{
  update;
  delete;
  field ( readonly ) TravelId, BookingId;
  field ( mandatory ) CarrierId, ConnectionId, FlightDate, BookingStatus;

  //Reconfirm that booking will create with travel draft
  association _Travel { with draft; }
  //Assure that child entity also managed with draft
  association _BookingSupplement { create; with draft; }

  //add a determination for the change in the booking also
  //TODO: Why the booking code is not triggering
  determination calculateTotalPrice on modify
            { field FlightPrice, CurrencyCode; }

  //Side effect
  side effects {
    field FlightPrice affects field _Travel.TotalPrice;
  }

  mapping for /dmo/booking_m{
    TravelId = travel_id;
    BookingId = booking_id;
    BookingDate = booking_date;
    CustomerId = customer_id;
    CarrierId = carrier_id;
    ConnectionId = connection_id;
    FlightDate = flight_date;
    FlightPrice = flight_price;
    CurrencyCode = currency_code;
    BookingStatus = booking_status;
    LastChangedAt = last_changed_at;
  }
}

//Define the behavior for child entity
define behavior for ZURS_BOOKSUPPL alias BookSuppl
//Make the behavior pool/implemetation for Booking Supplement
implementation in class zbp_urs_booksuppl unique
//Telling the framework to insert data in this db table for booking suppl
persistent table /dmo/booksuppl_m
draft table zurs_d_booksp
lock dependent by _Travel
authorization dependent by _Travel
etag master LastChangedAt
early numbering
{
  update;
  delete;
  field ( readonly ) BookingId, TravelId, BookingSupplementId;
  field ( mandatory ) Price, SupplementId;

  association _Travel { with draft; }
  association _Booking { with draft; }

  mapping for /dmo/booksuppl_m{
    TravelId = travel_id;
    BookingId = booking_id;
    BookingSupplementId = booking_supplement_id;
    SupplementId = supplement_id;
    Price = price;
    CurrencyCode = currency_code;
    LastChangedAt = last_changed_at;
  }
}